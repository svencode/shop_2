buildscript {
    repositories {
        jcenter()

        maven {
            url "http://maven.bughd.com/public"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
    }
}

apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    dexOptions {
        preDexLibraries false;
        incremental false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/com.squareup.okio/okio/pom.xml'
        exclude 'META-INF/maven/com.squareup.okio/okio/pom.properties'
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId "com.cqgk.shennong.shop"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    //签名
    signingConfigs {
        debug {
            storeFile file("keystore/debug.keystore")
        }
        release {
            storeFile file("keystore/waiter.jks")
            storePassword "waiter2016"
            keyAlias "waiter"
            keyPassword "waiter2016"
        }

    }

    //打包apk名称构成
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = outputFile.name.replace(".apk", "_${releaseTime()}.apk")
                output.outputFile = new File(
                        outputFile.parent + "/${variant.buildType.name}",
                        fileName)
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
            signingConfig signingConfigs.debug
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'org.xutils:xutils:3.3.34'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'de.greenrobot:eventbus:3.0.0-beta1'
    compile 'cn.finalteam:galleryfinal:1.4.8.7'
    compile 'com.baoyz.actionsheet:library:1.1.5'
    compile files('libs/alipaysdk.jar')
    compile files('libs/alipaysecsdk.jar')
    compile files('libs/alipayutdid.jar')
    compile files('libs/httpmime-4.1.3.jar')
    compile files('libs/SocialSDK_WeiXin_1.jar')
}
